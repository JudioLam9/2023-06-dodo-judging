dirk_y

high

# Ordering of user deposits changes impact of user quotas

## Summary
When a user tries to deposit in a vault there is a check to see if the user has a sufficient quota (i.e. how much can a user deposit). Because the used quota is calculated across all the tokens in the vault, the ordering of the user's deposits impacts their available quota of a token.

## Vulnerability Detail
When a user calls `userDeposit` (usually through the proxy) there is a check to ensure that the amount the user wants to deposit into the vault is within their quota allowance:

```solidity
require(ID3UserQuota(_USER_QUOTA_).checkQuota(user, token, amount), Errors.EXCEED_QUOTA);
```

A quota can be enabled and disabled independently for each token. During the `checkQuota` call, the currently used quota of the user is calculated across all of the tokens in the vault:

```solidity
      for (uint256 i = 0; i < tokenList.length; i++) {
            address _token = tokenList[i];
            (address assetDToken,,,,,,,,,,) = d3Vault.getAssetInfo(_token);
            uint256 tokenBalance = IERC20(assetDToken).balanceOf(user);
            if (tokenBalance > 0) {
                tokenBalance = tokenBalance.mul(d3Vault.getExchangeRate(token));
                (uint256 tokenPrice, uint8 priceDecimal) = ID3Oracle(d3Vault._ORACLE_()).getOriginalPrice(_token);
                usedQuota = usedQuota + tokenBalance * tokenPrice / 10 ** (priceDecimal+tokenDecimals);
            }
        }
```

Therefore, given the same quota for two different users (or a global quota for the token), one user with no deposits will have a greater available quota than a user that has already deposited, even if that deposit was for a different token.

As an example, let's assume that the quota for token X was 1000 USD and it was a global quota. User A has zero existing deposits, so they will be able to deposit 1000 USD worth of token X into the vault. However user B has 500 USD worth of existing deposits in token Y; this user will now only be able to deposit 500 USD worth of token X into the vault. If token Y doesn't have a quota then user A will be able to deposit freely as much of token Y as they would like after maximising the available quota of token X. They now have a greater share in the vault given the same parameters.

Below is a diff to the existing test suite that demonstrates this behaviour:

```diff
diff --git a/new-dodo-v3/test/DODOV3MM/D3Vault/periphery/D3UserQuota.t.sol b/new-dodo-v3/test/DODOV3MM/D3Vault/periphery/D3UserQuota.t.sol
index ae36231..a3cda14 100644
--- a/new-dodo-v3/test/DODOV3MM/D3Vault/periphery/D3UserQuota.t.sol
+++ b/new-dodo-v3/test/DODOV3MM/D3Vault/periphery/D3UserQuota.t.sol
@@ -26,6 +26,35 @@ contract D3UserQuotaTest is TestContext {
         assertEq(userQuota, type(uint256).max);
     }
 
+    function testQuotaIssue() public {
+        // Enable a global quota for token1 (however the same applies with non-global quotas)
+        d3UserQuota.enableQuota(address(token1), true);
+        d3UserQuota.enableGlobalQuota(address(token1), true);
+        d3UserQuota.setGlobalQuota(address(token1), uint256(1000));
+
+        // Mint tokens and approve dodo
+        // We have set the tokens to the same price for ease of demonstration
+        faucetToken(address(token1), user1, 1000 * 1e8);
+        faucetToken(address(token2), user2, 1000 * 1e8);
+        vm.prank(user1);
+        token1.approve(address(dodoApprove), type(uint256).max);
+        vm.prank(user2);
+        token2.approve(address(dodoApprove), type(uint256).max);
+
+        // User 1 deposits token 1 first
+        userDeposit(user1,address(token1), 1 * 1e6);
+        uint256 userQuota = d3UserQuota.getUserQuota(user1, address(token1));
+        assertEq(userQuota, 1000* 10 ** 8/ uint256(1300) - 1 * 1e6);
+
+        // User 2 deposits token 2 first
+        userDeposit(user2,address(token2), 1 * 1e6);
+        uint256 user2Quota = d3UserQuota.getUserQuota(user2, address(token1));
+        assertEq(user2Quota, 1000* 10 ** 8/ uint256(1300) - 1 * 1e6);
+
+        // Both users have the same quota available for token 1
+        assertEq(user2Quota, userQuota);
+    }
+
     function testEnableGlobalQuota() public {
         d3UserQuota.enableQuota(address(token1), true);
         d3UserQuota.enableGlobalQuota(address(token1), true);
diff --git a/new-dodo-v3/test/TestContext.t.sol b/new-dodo-v3/test/TestContext.t.sol
index 16d5312..9378f74 100644
--- a/new-dodo-v3/test/TestContext.t.sol
+++ b/new-dodo-v3/test/TestContext.t.sol
@@ -145,7 +145,7 @@ contract TestContext is Test {
         token3ChainLinkOracle = new MockChainlinkPriceFeed("Token3/USD", 18);
         token4ChainLinkOracle = new MockChainlinkPriceFeed("WETH/USD", 18);
         token1ChainLinkOracle.feedData(1300 * 1e18);
-        token2ChainLinkOracle.feedData(12 * 1e18);
+        token2ChainLinkOracle.feedData(1300 * 1e18);
         token3ChainLinkOracle.feedData(1 * 1e18);
         token4ChainLinkOracle.feedData(12 * 1e18);
         oracle.setPriceSource(

```

## Impact
Users that are aware of this behaviour will be able to order their deposits to maximise the amount of each token that they deposit into the vault. As a result they will be able to obtain a greater share of the borrow interest generated by pools and the protocol will be inherently unfair.

## Code Snippet
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L35
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L69-L92

## Tool used
Manual Review

## Recommendation
Current user quota usage should be calculated on a per-token basis rather than across all the tokens in the vault. So effectively the for loop quoted above should be removed and only the usage of the `token` argument should be calculated.