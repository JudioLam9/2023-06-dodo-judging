Sulpiride

high

# `removeD3Pool` always deletes the last element in the pool

## Summary
In D3Vault contract, `removeD3Pool` always deletes the last element in the pool instead of the pool specified in the function call

## Vulnerability Detail
Due to using the comparison operator `==` instead of the assigning operator `=`, `removeD3Pool` will fail copy the last pool before deleting it.

## Impact
Users won't be able to liquidate pools affected by this bug.

## Code Snippet
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3Vault.sol#L46

```solidity
function removeD3Pool(address pool) external onlyOwner {
    require(allPoolAddrMap[pool] == true, Errors.POOL_NOT_ADDED);
    ID3MM(pool).startLiquidation();

    allPoolAddrMap[pool] = false;
    _PENDING_REMOVE_POOL_ = pool;
    address creator = ID3MM(pool)._CREATOR_();
    address[] memory poolList = creatorPoolMap[creator];
    for (uint256 i = 0; i < poolList.length; i++) {
        if (poolList[i] == pool) {
            poolList[i] == poolList[poolList.length - 1]; // @audit double == ?
            creatorPoolMap[creator] = poolList;
            creatorPoolMap[creator].pop();
            emit RemovePool(pool);
            break;
        }
    }
}
```

## Tool used

Manual Review

## Recommendation
Use `=` instead of `==`

```diff
    for (uint256 i = 0; i < poolList.length; i++) {
        if (poolList[i] == pool) {
-           poolList[i] == poolList[poolList.length - 1];
+           poolList[i] = poolList[poolList.length - 1];
            creatorPoolMap[creator] = poolList;
            creatorPoolMap[creator].pop();
            emit RemovePool(pool);
            break;
        }
    }
```