0x4db5362c

medium

# Calculate used quota with wrong parameters

## Summary
`D3UserQuota#getUserQuota()` is intended to calculate the unused quota of user, which is the result of `globalQuota` subtracting `usedQuota`.  `usedQuota` is the sum of all used quotas. When calculating used quota for specific token , the wrong exhangeRate and tokenDecimals are used and cause the wrong calculation result of unused quota.

## Vulnerability Detail
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L69-L97

In [L80](https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L80), `_token` should be used as parameter to obtain the exchangeRate, however `token` was used.
In [L82](https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L82), tokenDecimals should be the decimal of `_token`, however the decimal of `token` was used for all tokens.

## Impact
User still can deposit some of tokens even the sum of used quota has reach the max limit. check `testGetUserQuotaNoChangeDuetoUserDeposit()` in Proof of concept for detail.

User couldn't deposit some of tokens because of wrong calculation. check `testGetUserQuotaChangeToZeroWithSmallDeposit()` in Proof of concept for detail.

## Code Snippet
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L69-L97

## Tool used

Manual Review

## Proof of concept

Save the below codes to test/DODOV3MM/D3Vault/phriphery/D3UserQuotaErrorTest.t.sol
Run forge test --match-path test/DODOV3MM/D3Vault/periphery/D3UserQuotaErrorTest.t.sol to verify the result.

    /*
        SPDX-License-Identifier: Apache-2.0*/

    pragma solidity 0.8.16;

    import "../../../TestContext.t.sol";
    import {Errors as VaultErrors} from "contracts/DODOV3MM/D3Vault/Errors.sol";
    contract D3UserQuotaErrorTest is TestContext {

        using DecimalMath for uint256;

        function setUp() public {
            createTokens();
            createD3Oracle();
            createD3RateManager();
            createD3VaultTwo();
            createD3MMFactory();
            createD3Proxy();

            vm.prank(vaultOwner);
            d3Vault.setNewD3Factory(address(d3MMFactory));
            createD3MM();
            
            createRouter();
            createLiquidatorAdapter();
            vm.prank(vaultOwner);
            d3Vault.addRouter(address(liquidationRouter));

            token1.mint(user1, 1000 * 1e8);
            token2.mint(user1, 1000 * 1e18);
            vm.prank(user1);
            token1.approve(address(d3Vault), type(uint256).max);
            token2.approve(address(d3Vault), type(uint256).max);
        }  

        function testGetUserQuotaNoChangeDuetoUserDeposit() public {
            vm.prank(user1);
            token1.approve(address(dodoApprove), type(uint256).max);
            vm.prank(user1);
            token2.approve(address(dodoApprove), type(uint256).max);

            //enable global quota for token1 and set it to 1300 (the price of 1 token1 in virtual USD)
            d3UserQuota.enableQuota(address(token1), true);
            d3UserQuota.enableGlobalQuota(address(token1), true);
            d3UserQuota.setGlobalQuota(address(token1), uint256(1300));   


            uint userQuotaForToken1 = d3UserQuota.getUserQuota(user1, address(token1));
            assertEq(userQuotaForToken1, 1e8);

            //enable global quota for token2 and set it to 12 (the price of 1 token2 in virtual USD)
            d3UserQuota.enableQuota(address(token2), true);
            d3UserQuota.enableGlobalQuota(address(token2), true);
            d3UserQuota.setGlobalQuota(address(token2), uint256(12));   

            uint userQuotaForToken2 = d3UserQuota.getUserQuota(user1, address(token2));
            assertEq(userQuotaForToken2, 1e18);


            //user1 deposits 1 token1 to d3Vault
            vm.prank(user1);
            d3Proxy.userDeposit(user1, address(token1), 1e8);

            userQuotaForToken1 = d3UserQuota.getUserQuota(user1, address(token1));
            assertEq(userQuotaForToken1, 0);
            userQuotaForToken2 = d3UserQuota.getUserQuota(user1, address(token2));
            assertEq(userQuotaForToken2, 1e18);
        }      

        function testGetUserQuotaChangeToZeroWithSmallDeposit() public {
            vm.prank(user1);
            token1.approve(address(dodoApprove), type(uint256).max);
            vm.prank(user1);
            token2.approve(address(dodoApprove), type(uint256).max);

            //enable global quota for token1 and set it to 3900 (the price of 3 token1 in virtual USD)
            d3UserQuota.enableQuota(address(token1), true);
            d3UserQuota.enableGlobalQuota(address(token1), true);
            d3UserQuota.setGlobalQuota(address(token1), uint256(3900));   


            uint userQuotaForToken1 = d3UserQuota.getUserQuota(user1, address(token1));
            assertEq(userQuotaForToken1, 3e8);

            //enable global quota for token2 and set it to 12 (the price of 1 token2 in virtual USD)
            d3UserQuota.enableQuota(address(token2), true);
            d3UserQuota.enableGlobalQuota(address(token2), true);
            d3UserQuota.setGlobalQuota(address(token2), uint256(12));   

            uint userQuotaForToken2 = d3UserQuota.getUserQuota(user1, address(token2));
            assertEq(userQuotaForToken2, 1e18);

            //user1 deposits 0.0000000325 token2 to d3Vault, which only values 0.00000039 in virtual USD
            vm.prank(user1);
            d3Proxy.userDeposit(user1, address(token2), 325e8);

            //the initial quota of token1 for user1 is 1300USD(3e8 in amount), but it is changed to 0 after a small amount of token2 was deposited.
            userQuotaForToken1 = d3UserQuota.getUserQuota(user1, address(token1));
            assertEq(userQuotaForToken1, 0);
        }
    }

- `testGetUserQuotaNoChangeDuetoUserDeposit()`: user1 deposits 1 token1 in d3Vault, it's reasonable that the unused quota is 0 after depositing. But when calculating unused quota in `d3UserQuota.getUserQuota(user1, address(token2))`, the used quota of token1 will be zero because misused `tokenDecimals` is 18. The calculation in [L82](https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L82) is described as below:

     usedQuota = 0 + 1e8 * 1300e18 / 10 ** (18+18);

- `testGetUserQuotaChangeToZeroWithSmallDeposit()`: user1 deposits 0.0000000325 token2 in d3Vault, which only values 0.00000039 in virtual USD. it's reasonable that the unused quota is far from zero after depositing. But when calculating unused quota in `d3UserQuota.getUserQuota(user1, address(token1))`, the used quota of token2 will be very big because misused `tokenDecimals` is 8. The calculation in [L82](https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L82) is described as below:

     usedQuota = 0 + 325e8 * 12e18 / 10 ** (18+8);


## Recommendation
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L80-L82
Change the codes as below:

      tokenBalance = tokenBalance.mul(d3Vault.getExchangeRate(_token));
      (uint256 tokenPrice, uint8 priceDecimal) = ID3Oracle(d3Vault._ORACLE_()).getOriginalPrice(_token);
      usedQuota = usedQuota + tokenBalance * tokenPrice / 10 ** (priceDecimal+IERC20(_token).decimals());


