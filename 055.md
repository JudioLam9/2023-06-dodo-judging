skyge

medium

# Division by zero vulnerability in getUtilizationRatio() function when cash and reserves are equal

## Summary

The `getUtilizationRatio()` function in the contract `D3VaultFunding.sol` can revert due to division by zero in certain cases. This occurs when the `cash` and `reserves` variables are equal, this means all borrows from reserves.

## Vulnerability Detail

The vulnerable code is: 
```solidity
function getUtilizationRatio(address token) public view returns (uint256) {
    uint256 borrows = getTotalBorrows(token);
    uint256 cash = getCash(token);
    uint256 reserves = getReservesInVault(token);
    if (borrows == 0) return 0;
    return borrows.div(cash + borrows - reserves);
}
```
The function does not check for division by zero in the case where `cash + borrows - reserves` is zero. 

One possible case:
```solidity
function testBorrowFromReserves() public {
    // user1 deposit 10 into vault
    // pool has 100 as collateral, then borrow 10
    // after 10s, pools repay all.
    // user1 with 10 from vault
    // pool tries to borrow 1 wei.
    vm.prank(user1);
    token1.approve(address(dodoApprove), type(uint256).max);

    mockUserQuota.setUserQuota(user1, address(token1), 1000 * 1e8);
    vm.prank(user1);
    d3Proxy.userDeposit(user1, address(token1), 10 * 1e8);

    token1.mint(address(d3MM), 100 * 1e8);
    poolBorrow(address(d3MM), address(token1), 10 * 1e8);

    // pass 10s
    vm.warp(10 + 1);

    vm.startPrank(address(d3MM));

    token1.approve(address(d3Vault), type(uint256).max);

    d3Vault.accrueInterest(address(token1));
    uint256 repayAmount = d3Vault.getTotalBorrows(address(token1));

    d3Vault.poolRepay(address(token1), repayAmount);
    assertEq(d3Vault.getTotalBorrows(address(token1)), 0);

    vm.stopPrank();

    userWithdraw(user1, address(token1), 10 * 1e8);

    poolBorrow(address(d3MM), address(token1), 1);
}
```

Output is:

<img width="892" alt="image" src="https://github.com/sherlock-audit/2023-06-dodo-skyge/assets/27282380/1e88ffc8-7793-4695-b717-5ed7618f94a4">


## Impact
This can cause the function to revert and prevent admin from performing certain operations, such as `borrow()`.

## Code Snippet

https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L208

## Tool used

Manual Review

## Recommendation

One possible way:

```solidity
function getUtilizationRatio(address token) public view returns (uint256) {
    uint256 borrows = getTotalBorrows(token);
    uint256 cash = getCash(token);
    uint256 reserves = getReservesInVault(token);
    if (borrows == 0) return 0;
    if (cash + borrows <= reserves) return 1e18;    // Utilization Ratio is 100%
    return borrows.div(cash + borrows - reserves);
}
```
