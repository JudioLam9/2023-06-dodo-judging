0x4db5362c

medium

# An error of overflow will be throned and cause unexpected behavior of function.

## Summary
An error of overflow could be throned when `D3UserQuota.sol#getUserQuota()` is called, which causes `getUserQuota()` return nothing except error.
## Vulnerability Detail
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L89-L93
In the lines above, the calculation of unused quota doesn't compare two numbers before  subtraction, which will cause the function `getUserQuota()` throws an error.
## Impact
When `D3VaultFunding#userDeposit()` is called, the function could be reverted and no any specific error is throned out. But the design was intended to throw an error `Errors.EXCEED_QUOTA`

## Code Snippet
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L89-L93

      if (isGlobalQuota[token]) {
          return (gloablQuota[token] - usedQuota) * 10 ** (_priceDecimal + tokenDecimals) / _tokenPrice;
      } else {
          return (calculateQuota(user) - usedQuota) * 10 ** (_priceDecimal + tokenDecimals) / _tokenPrice;
      }
## Tool used

Manual Review

## Proof of concept

`user1` deposits enough `token1` to reach the quota limit of token1.   Calling `D3UserQuota#getUserQuota()` will return 0 at this time, which means the used quota of `user1` is equal to the quota of `token1`. After a while call `D3UserQuota#getUserQuota()`  again, it will revert with overflow error.

Save the below codes to `test/DODOV3MM/D3Vault/phriphery/D3UserQuotaErrorTest.t.sol`
Run `forge test --match-path test/DODOV3MM/D3Vault/periphery/D3UserQuotaErrorTest.t.sol` to verify the result.

    /*
        SPDX-License-Identifier: Apache-2.0*/

    pragma solidity 0.8.16;

    import "../../../TestContext.t.sol";

    contract D3UserQuotaErrorTest is TestContext {

        using DecimalMath for uint256;

        function setUp() public {
            createTokens();
            createD3Oracle();
            createD3RateManager();
            createD3VaultTwo();
            createD3MMFactory();
            createD3Proxy();

            vm.prank(vaultOwner);
            d3Vault.setNewD3Factory(address(d3MMFactory));
            createD3MM();
            
            createRouter();
            createLiquidatorAdapter();
            vm.prank(vaultOwner);
            d3Vault.addRouter(address(liquidationRouter));

            token1.mint(user1, 1000 * 1e8);
            token1.mint(user2, 1000 * 1e8);
            vm.prank(user1);
            token1.approve(address(d3Vault), type(uint256).max);
            vm.prank(user2);
            token1.approve(address(d3Vault), type(uint256).max);
        }  

    function testGetUserQuotaOverflow() public {
            vm.prank(user1);
            token1.approve(address(dodoApprove), type(uint256).max);

            //enable global quota for token1 and set it to 1300*5 (the price of 5 token1 in virtual USD)
            d3UserQuota.enableQuota(address(token1), true);
            d3UserQuota.enableGlobalQuota(address(token1), true);
            d3UserQuota.setGlobalQuota(address(token1), uint256(1300*5));   

            //user1 deposits 5 token1 to d3Vault     
            vm.prank(user1);
            d3Proxy.userDeposit(user1, address(token1), 5 * 1e8);

            //pool borrows 1 token1 from d3Vault
            token1.mint(address(d3MM), 1 * 1e8);
            poolBorrow(address(d3MM), address(token1), 1 * 1e8);

            //since the global quota has been reached, the left user quota would be 0
            uint userQuota = d3UserQuota.getUserQuota(user1, address(token1));
            assertEq(userQuota, 0);
            // pass one year
            vm.warp(31536000 + 1);
            //the used quota would be increased because of cumulative interest gained, which will cause the overflow error when calculating user quota.
            vm.expectRevert(stdError.arithmeticError);
            d3UserQuota.getUserQuota(user1, address(token1));    
        }    
    }

## Recommendation
Compare the numbers before subtraction. Change the codes as below:

            if (isGlobalQuota[token]) {
                if (gloablQuota[token] > usedQuota) {
                    return (gloablQuota[token] - usedQuota) * 10 ** (_priceDecimal + tokenDecimals) / _tokenPrice;//@audit-info check implementation carefully
                } else {
                    return 0;
                }
            } else {
                uint256 quota = calculateQuota(user);
                if (quota > usedQuota) {
                    return (quota - usedQuota) * 10 ** (_priceDecimal + tokenDecimals) / _tokenPrice;//@audit-info check implementation carefully
                } else {
                    return 0;
                }
            }

