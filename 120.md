osmanozdemir1

high

# An attacker can burn the whole `dToken` balance in `D3Proxy` and withdraw the corresponding amount of token from the vault as `dToken` address passed by the attacker is never checked

## Summary
Users are allowed to withdraw their tokens directly from the vault or through the `D3Proxy` contract. User's `dToken`'s will be burnt as expected if they use the vault directly. But if they want to use the D3Proxy, they have to deposit dTokens to the proxy first. The issue is that the `dToken` address passed by the user is never checked and an attacker can pass any address as `dToken` address. 

## Vulnerability Detail
Users can withdraw their tokens directly by calling the `userWithdraw()` function in the `D3VaultFunding.sol` contract, or they can prefer to use the `D3Proxy.sol` contract for this process.

Let's examine the `userWithdraw()` function:
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L47-L67

```solidity
//File: D3VaultFunding.sol 
47.    /// @param to who receive tokens
48.    /// @param user who pay dTokens
49.    /// @param token original token address
50.    /// @param dTokenAmount dtoken the token record amount
51.    function userWithdraw(address to, address user, address token, uint256 dTokenAmount) external nonReentrant allowedToken(token) returns(uint256 amount) {
52.        accrueInterest(token);
53.        AssetInfo storage info = assetInfo[token];
54.-->     require(dTokenAmount <= IDToken(info.dToken).balanceOf(msg.sender), Errors.DTOKEN_BALANCE_NOT_ENOUGH);
55.
56.        amount = dTokenAmount.mul(_getExchangeRate(token));
57.-->     IDToken(info.dToken).burn(msg.sender, dTokenAmount);
58.        IERC20(token).safeTransfer(to, amount);
59.        info.balance = info.balance - amount;
60.
61.        // used for calculate user withdraw amount
62.        // this function could be called from d3Proxy, so we need "user" param
63.        // In the meantime, some users may hope to use this function directly,
64.        // to prevent these users fill "user" param with wrong addresses,
65.        // we use "msg.sender" param to check.
66.        emit UserWithdraw(msg.sender, user, token, amount);
67.    }
```

This function can be called directly or called by D3Proxy. As you can see in the developer's comments in lines 61-65, developers already thought that an attacker might try to pass other people's addresses as `user`, and because of that they check the `msg.sender` balance and they burn `dTokens` from the `msg.sender`.

When it is called directly, the function will check if the user(*msg.sender*) has enough balance of `dToken`, it will burn `dToken`'s from the user and will send the corresponding amount of tokens to the "to" address.

If this function is called by the `D3Proxy`, `msg.sender` will be the proxy contract. The flow is like this: Users deposit their `dToken`'s to the proxy contract. Then the proxy contract calls this function. `dToken`'s in the proxy will be burnt and tokens will be sent to the "to" address. Here is the function in the proxy:  
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/periphery/D3Proxy.sol#L160-L168

```solidity
//File: D3Proxy.sol
160.    function userWithdraw(address to, address token, address dToken, uint256 dTokenAmount) external payable returns(uint256 amount){
161.-->     _deposit(msg.sender, address(this), dToken, dTokenAmount);
162.        if (token != _ETH_ADDRESS_) {
163.            amount = ID3Vault(_D3_VAULT_).userWithdraw(to, msg.sender, token, dTokenAmount);
164.        } else {
165.            amount = ID3Vault(_D3_VAULT_).userWithdraw(address(this), msg.sender, _WETH_, dTokenAmount);
166.            _withdrawWETH(to, amount);
167.        }
168.    }
```

As you can see in line 161 above, `dToken`'s will be deposited to the D3Proxy contract. The vulnerability is that **the** `dToken` **address passed by the user is never checked**.  
A user can type any address and deposit any token to the proxy contract, but the proxy contract will call the vault anyways. In the vault contract, the real `dToken`'s will be burnt **from the proxy**(*msg.sender*) because the vault contract actually calls the `info.dToken` address, which is the corresponding `dToken` for the token.

Here is the attack scenario to drain the contract:

1. Check the `dToken` balances of the D3Proxy contract.  
    Let's assume you are the attacker and want to withdraw `tokenA`, and the `dTokenA` balance of the proxy is 1000.
    
2. Deploy a simple ERC20 token contract and mint thousands of tokens for yourself.
    
3. Call the `userWithdraw()` function in the `D3Proxy` with your newly deployed, worthless attack token's address. A simple example:
    
    ```solidity
    //approve the proxy first for the _deposit() call
    IERC20(attackToken).approve(d3proxy, 1000)
    d3proxy.userWithdraw(to, tokenA, attackToken, 1000);
    ```
    
4. The proxy contract will take your worthless `attackToken`'s and will call the vault contract.
    
5. The vault contract will burn the proxy's real `dToken`'s and will send you `tokenA`'s.
    
6. Keep doing this for every different `dToken`'s in the proxy. `dTokenB`, `dTokenC`, ... , `dTokenZ`.
    


## Impact
An attacker can cause burning of all the `dToken`'s in the D3Proxy and withdraw the corresponding amount of tokens from the vault with a worthless erc20 token. 

## Code Snippet
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/periphery/D3Proxy.sol#L160-L168

```solidity
//File: D3Proxy.sol
160.    function userWithdraw(address to, address token, address dToken, uint256 dTokenAmount) external payable returns(uint256 amount){
161.-->     _deposit(msg.sender, address(this), dToken, dTokenAmount);
162.        if (token != _ETH_ADDRESS_) {
163.            amount = ID3Vault(_D3_VAULT_).userWithdraw(to, msg.sender, token, dTokenAmount);
164.        } else {
165.            amount = ID3Vault(_D3_VAULT_).userWithdraw(address(this), msg.sender, _WETH_, dTokenAmount);
166.            _withdrawWETH(to, amount);
167.        }
168.    }
```

https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L47-L67

```solidity
//File: D3VaultFunding.sol 
47.    /// @param to who receive tokens
48.    /// @param user who pay dTokens
49.    /// @param token original token address
50.    /// @param dTokenAmount dtoken the token record amount
51.    function userWithdraw(address to, address user, address token, uint256 dTokenAmount) external nonReentrant allowedToken(token) returns(uint256 amount) {
52.        accrueInterest(token);
53.        AssetInfo storage info = assetInfo[token];
54.-->     require(dTokenAmount <= IDToken(info.dToken).balanceOf(msg.sender), Errors.DTOKEN_BALANCE_NOT_ENOUGH);
55.
56.        amount = dTokenAmount.mul(_getExchangeRate(token));
57.-->     IDToken(info.dToken).burn(msg.sender, dTokenAmount);
58.        IERC20(token).safeTransfer(to, amount);
59.        info.balance = info.balance - amount;
60.
61.        // used for calculate user withdraw amount
62.        // this function could be called from d3Proxy, so we need "user" param
63.        // In the meantime, some users may hope to use this function directly,
64.        // to prevent these users fill "user" param with wrong addresses,
65.        // we use "msg.sender" param to check.
66.        emit UserWithdraw(msg.sender, user, token, amount);
67.    }
```

## Tool used

Manual Review

## Recommendation
I would recommend passing the user input `dToken` address value to the `D3VaultFunding.sol` with the contract call and check if it is actually the `info.dToken` or not. For example:

```solidity
//File D3Proxy.sol
    // ... other parts of the function etc.
    // pass the dToken address value to the vault 
163.    amount = ID3Vault(_D3_VAULT_).userWithdraw(to, msg.sender, token, dTokenAmount, dToken);
    // pass the dToken address value to the vault
165.    amount = ID3Vault(_D3_VAULT_).userWithdraw(to, msg.sender, token, dTokenAmount, dToken);

// ------------------------------------------------------------

// File D3VaultFunding.sol
    // get the dToken address too as a function param, and compare.
    function userWithdraw(address to, address user, address token, uint256 dTokenAmount, address dToken) external nonReentrant allowedToken(token) returns(uint256 amount) {
        accrueInterest(token);
        AssetInfo storage info = assetInfo[token];
+++     if (info.dToken != dToken) revert //add a custom error.
    // .... rest of the function
}
```