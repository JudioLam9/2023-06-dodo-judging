0x4db5362c

medium

# DODO protocol could be unavailable due to A Deny Of Service with block gas limit

## Summary
A uncontrolled use of loop could cause a huge amount of gas consumption and trigger a Deny Of Service with block gas limit, thereafter the critical functions of the whole protocol may be unavailable and there is no way to recover the whole protocol back to available status. 

## Vulnerability Detail

- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L250-L259
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L261-L267
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L274-L290
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L357-L364
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L372-L389
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L392-L413
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultLiquidation.sol#L65-L85
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultLiquidation.sol#L117-L152

All above functions uses a loop to calculate the result. The count of loop depends on the length of `tokenList`.  It's obvious that the protocol is willing to add support for more tokens, which means the length of `tokenList` will be increased rapidly, and the gas consumption of above functions will grow rapidly. At some point, the gas consumption may exceed the block gas limit, and the whole function will be reverted, which is a classical [DoS With Block Gas Limit](https://swcregistry.io/docs/SWC-128). 

## Impact
Any Functions that directly or indirectly call the above functions will be reverted. 
- `D3Trading#sellToken()`
- `D3Trading#buyToken()`
- `D3Funding#borrow()`
- `D3Funding#repay()`
- `D3Funding#repayAll()`
- `D3Funding#makerDeposit()`
- `D3Funding#makerWithdraw()`
- `D3Funding#checkCanBeLiquidated()`
- `D3Funding#checkBorrowSafe()`
- `D3VaultLiquidation#startLiquidation()`
- `D3VaultLiquidation#liquidate()`
- `D3VaultLiquidation#finishLiquidation()`

The critical functions of the whole protocol may be unavailable due to block gas limit:

- User can not sell or buy tokens from pool
- Pool can not borrow tokens from vault
- Pool can not repay or repay all tokens to vault
- No one can call `D3Funding#makerDeposit()` successfully after transferring tokens to pool
- Maker can not withdraw tokens from pool
- DODO team can not liquidate pool even it is eligible to be liquidated.
- No one cal call `D3VaultLiquidation#liquidate()` to liquidate pool even it is eligible.


Since there is no way to remove new added `token` from `tokenList` in vault, the whole protocol can not be recovered to normal status.

The owner of the vault can call `D3Vault#pendingRemovePoolRepayAll()` to force pool repay all debt if the balance is no less than the debt, but the pool is no longer available.


## Code Snippet
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L250-L259
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L261-L267
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L274-L290
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L357-L364
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L372-L389
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L392-L413
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultLiquidation.sol#L65-L85
- https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultLiquidation.sol#L117-L152
## Tool used

Manual Review

## Recommendation
Use another list to store all tokens which are used by pool:

https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/intf/ID3Vault.sol
Defines a new function:

    function updatePoolToken(address pool, address token) external;

https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultStorage.sol
Add  `poolTokenList` and `isPoolToken` in D3VaultStorage.sol:

    mapping(address => address[]) poolTokenList; //pool => tokens
    mapping(address => mapping(address => bool)) isPoolToken; //pool => token => bool

https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol

Add new functions in D3VaultFunding.sol:

    function updatePoolToken(address pool, address token) external nonReentrant {
        _updatePoolToken(pool, token);
    }
    function _updatePoolToken(address pool, address token) internal {
        if (isPoolToken[pool][token]&&
        (IERC20(token).balanceOf(pool) == 0)&&
        (assetInfo[token].borrowRecord[pool].amount == 0)) {
            address[] memory poolTokens = poolTokenList[pool];
            for (uint256 i = 0; i < poolTokens.length; i++) {
                if (poolTokens[i] == token) {
                    poolTokens[i] = poolTokens[poolTokens.length - 1];
                    poolTokenList[pool] = poolTokens;
                    poolTokenList[pool].pop();
                    break;
                }
            }
        } else if (!isPoolToken[pool][token]&&
        ((IERC20(token).balanceOf(pool) != 0)||(assetInfo[token].borrowRecord[pool].amount != 0))) {
                isPoolToken[pool][token] = true;
                poolTokenList[pool].push(token);
        }
    }

Replace all `tokenList` with `poolTokenList[pool]` in all functions below:

    function _getTotalDebtValue(address pool) internal view returns (uint256 totalDebt) {
        address[] memory poolTokens = poolTokenList[pool];//@audit-info use poolTokenList[pool] to calculate result
        for (uint256 i = 0; i < poolTokens.length; i++) {//@audit-info use poolTokenList[pool] to calculate result
            address token = poolTokens[i];//@audit-info use poolTokenList[pool] to calculate result
            AssetInfo storage info = assetInfo[token];
            BorrowRecord memory record = info.borrowRecord[pool];
            uint256 borrows = record.amount.div(record.interestIndex == 0 ? 1e18 : record.interestIndex).mul(info.borrowIndex);
            uint256 price = ID3Oracle(_ORACLE_).getPrice(token);
            totalDebt += borrows.mul(price);
        }
    }

    function getTotalAssetsValue(address pool) public view returns (uint256 totalValue) {
        address[] memory poolTokens = poolTokenList[pool];//@audit-info use poolTokenList[pool] to calculate result
        for (uint256 i = 0; i < poolTokens.length; i++) {//@audit-info use poolTokenList[pool] to calculate result
            address token = poolTokens[i];//@audit-info use poolTokenList[pool] to calculate result
            uint256 price = ID3Oracle(_ORACLE_).getPrice(token);
            totalValue += DecimalMath.mul(ID3MM(pool).getTokenReserve(token), price);
        }
    }

    function _getCollateralRatio(address pool) internal view returns (uint256) {
        uint256 collateral = 0;
        uint256 debt = 0;
        address[] memory poolTokens = poolTokenList[pool];//@audit-info use poolTokenList[pool] to calculate result
        for (uint256 i; i < poolTokens.length; i++) {//@audit-info use poolTokenList[pool] to calculate result
            address token = poolTokens[i];//@audit-info use poolTokenList[pool] to calculate result
            AssetInfo storage info = assetInfo[token];

            (uint256 balance, uint256 borrows) = _getBalanceAndBorrows(pool, token);
            uint256 price = ID3Oracle(_ORACLE_).getPrice(token);
            if (balance >= borrows) {
                collateral += min(balance - borrows, info.maxCollateralAmount).mul(info.collateralWeight).mul(price);
            } else {
                debt += (borrows - balance).mul(info.debtWeight).mul(price);
            }
        }
        return _ratioDiv(collateral, debt);
    }

    function getTotalDebtValue(address pool) external view returns (uint256 totalDebt) {
        address[] memory poolTokens = poolTokenList[pool];//@audit-info use poolTokenList[pool] to calculate result
        for (uint256 i = 0; i < poolTokens.length; i++) {//@audit-info use poolTokenList[pool] to calculate result
            address token = poolTokens[i];//@audit-info use poolTokenList[pool] to calculate result
            uint256 borrowAmount = getPoolBorrowAmount(pool, token);
            uint256 price = ID3Oracle(_ORACLE_).getPrice(token);
            totalDebt += borrowAmount.mul(price);
        }
    }

    function getCollateralRatio(address pool) public view returns (uint256) {
        uint256 collateral = 0;
        uint256 debt = 0;
        address[] memory poolTokens = poolTokenList[pool];//@audit-info use poolTokenList[pool] to calculate result
        for (uint256 i; i < poolTokens.length; i++) {//@audit-info use poolTokenList[pool] to calculate result
            address token = poolTokens[i];//@audit-info use poolTokenList[pool] to calculate result
            AssetInfo storage info = assetInfo[token];

            (uint256 balance, uint256 borrows) = getBalanceAndBorrows(pool, token);
            uint256 price = ID3Oracle(_ORACLE_).getPrice(token);
            
            if (balance >= borrows) {
                collateral += min(balance - borrows, info.maxCollateralAmount).mul(info.collateralWeight).mul(price);
            } else {
                debt += (borrows - balance).mul(info.debtWeight).mul(price);
            }
        }
        return _ratioDiv(collateral, debt);
    }

    function getCollateralRatioBorrow(address pool) public view returns (uint256) {
        uint256 balanceSumPositive = 0;
        uint256 balanceSumNegative = 0;
        uint256 borrowedSum = 0;
        address[] memory poolTokens = poolTokenList[pool];//@audit-info use poolTokenList[pool] to calculate result
        for (uint256 i; i < poolTokens.length; i++) {//@audit-info use poolTokenList[pool] to calculate result
            address token = poolTokens[i];//@audit-info use poolTokenList[pool] to calculate result

            (uint256 balance, uint256 borrows) = getBalanceAndBorrows(pool, token);
            uint256 price = ID3Oracle(_ORACLE_).getPrice(token);

            if (balance >= borrows) {
                balanceSumPositive += min(balance - borrows, assetInfo[token].maxCollateralAmount).mul(price);
            } else {
                balanceSumNegative += (borrows - balance).mul(price);
            }

            borrowedSum += borrows.mul(price);
        }
        
        uint256 balanceSum = balanceSumPositive < balanceSumNegative ? 0 : balanceSumPositive - balanceSumNegative;
        return _ratioDiv(balanceSum, borrowedSum);
    }


Add `_updatePoolToken()` call in the functions below:

    function poolBorrow(address token, uint256 amount) external nonReentrant allowedToken(token) onlyPool {
    ...
        _updatePoolToken(msg.sender, token);//@audit-info call _updatePoolToken() before emit event
        emit PoolBorrow(msg.sender, token, amount, interests);
    }

    function poolRepay(address token, uint256 amount) external nonReentrant allowedToken(token) onlyPool {
    ...
        _updatePoolToken(msg.sender, token);//@audit-info call _updatePoolToken() before emit event
        emit PoolRepay(msg.sender, token, amount, interests);
    }

    function _poolRepayAll(address pool, address token) internal {
    ...
        _updatePoolToken(pool, token);//@audit-info call _updatePoolToken() before emit event
        emit PoolRepay(pool, token, amount, interests);
    }

https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultLiquidation.sol

Add `_updatePoolToken()` call in the functions below:

    function liquidate(
        address pool,
        address collateral,
        uint256 collateralAmount,
        address debt,
        uint256 debtToCover
    ) external nonReentrant {
    ...
        _updatePoolToken(pool, debt);//@audit-info call _updatePoolToken() before exiting the function
    }

    function liquidateByDODO(
        address pool,
        LiquidationOrder calldata order,
        bytes calldata routeData,
        address router
    ) external onlyLiquidator nonReentrant {
    ...
        _updatePoolToken(pool, order.fromToken);//@audit-info call _updatePoolToken() before exiting the function
        _updatePoolToken(pool, order.toToken);//@audit-info call _updatePoolToken() before exiting the function
    }

Replace all `tokenList` with `poolTokenList[pool]` in all functions below:

    function startLiquidation(address pool) external onlyLiquidator nonReentrant {
        accrueInterests();

        require(!ID3MM(pool).isInLiquidation(), Errors.ALREADY_IN_LIQUIDATION);
        require(checkCanBeLiquidatedAfterAccrue(pool), Errors.CANNOT_BE_LIQUIDATED);
        ID3MM(pool).startLiquidation();

        uint256 totalAssetValue = getTotalAssetsValue(pool);
        uint256 totalDebtValue = _getTotalDebtValue(pool);
        require(totalAssetValue < totalDebtValue, Errors.NO_BAD_DEBT);

        uint256 ratio = totalAssetValue.div(totalDebtValue);

        address[] memory poolTokens = poolTokenList[pool];//@audit-info use poolTokenList[pool] to calculate result
        for (uint256 i; i < poolTokens.length; i++) {//@audit-info use poolTokenList[pool] to calculate result
            address token = poolTokens[i];//@audit-info use poolTokenList[pool] to calculate result
            AssetInfo storage info = assetInfo[token];
            BorrowRecord storage record = info.borrowRecord[pool];
            uint256 debt = record.amount.div(record.interestIndex == 0 ? 1e18 : record.interestIndex).mul(info.borrowIndex).mul(ratio);
            liquidationTarget[pool][token] = debt; 
        }
    }

    function finishLiquidation(address pool) external onlyLiquidator nonReentrant {
        require(ID3MM(pool).isInLiquidation(), Errors.NOT_IN_LIQUIDATION);
        accrueInterests();

        bool hasPositiveBalance;
        bool hasNegativeBalance;
        address[] memory poolTokens = poolTokenList[pool];//@audit-info use poolTokenList[pool] to calculate result
        for (uint256 i; i < poolTokens.length; i++) {//@audit-info use poolTokenList[pool] to calculate result
            address token = poolTokens[i];//@audit-info use poolTokenList[pool] to calculate result

            AssetInfo storage info = assetInfo[token];
            uint256 balance = IERC20(token).balanceOf(pool);
            uint256 debt = liquidationTarget[pool][token];
            int256 difference = int256(balance) - int256(debt);
            if (difference > 0) {
                require(!hasNegativeBalance, Errors.LIQUIDATION_NOT_DONE);
                hasPositiveBalance = true;
            } else if (difference < 0) {
                require(!hasPositiveBalance, Errors.LIQUIDATION_NOT_DONE);
                hasNegativeBalance = true;
                debt = balance; // if balance is less than target amount, just repay with balance
            }

            BorrowRecord storage record = info.borrowRecord[pool];
            uint256 borrows = record.amount;
            if (borrows == 0) continue;

            // note: During liquidation process, the pool's debt will slightly increase due to the generated interests. 
            // The liquidation process will not repay the interests. Thus all dToken holders will share the loss equally.
            uint256 realDebt = borrows.div(record.interestIndex == 0 ? 1e18 : record.interestIndex).mul(info.borrowIndex);
            IERC20(token).transferFrom(pool, address(this), debt);//@audit-info what happen to remain balance after repay?

            info.totalBorrows = info.totalBorrows - realDebt;
            record.amount = 0;
            _updatePoolToken(pool, token);//@audit-info call _updatePoolToken()
        }
        ID3MM(pool).finishLiquidation();
    }

https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Pool/D3Funding.sol
Add `ID3Vault(state._D3_VAULT_).updatePoolToken(address(this), token)` call in the functions below:

    function makerDeposit(address token) external nonReentrant poolOngoing {
        ...
        _updateReserve(token);
        ID3Vault(state._D3_VAULT_).updatePoolToken(address(this), token);//@audit-info add function call after _updateReserve()
        ...
    }

    function makerWithdraw(address to, address token, uint256 amount) external onlyOwner nonReentrant poolOngoing {
        ...
        _updateReserve(token);
        ID3Vault(state._D3_VAULT_).updatePoolToken(address(this), token);//@audit-info add function call after _updateReserve()
        ...
    }

https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Pool/D3Trading.sol
Add `ID3Vault(state._D3_VAULT_).updatePoolToken(address(this), token)` call in the functions below:

    function _recordSwap(address fromToken, address toToken, uint256 fromAmount, uint256 toAmount) internal {
        tokenCumMap[fromToken].cumulativeBid += fromAmount;
        tokenCumMap[toToken].cumulativeAsk += toAmount;

        _updateReserve(fromToken);
        _updateReserve(toToken);
        ID3Vault(state._D3_VAULT_).updatePoolToken(address(this), fromToken);//@audit-info add function call after _updateReserve()
        ID3Vault(state._D3_VAULT_).updatePoolToken(address(this), toToken);//@audit-info add function call after _updateReserve()
    }
