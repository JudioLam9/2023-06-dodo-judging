0x4db5362c

high

# User's assets could be locked in vault forever because of wrong calculation in D3VaultFunding#poolRepayAll()

## Summary
The token balance in vault is wrongly calculated in `poolRepayAll()`. When calling `poolRepayAll()`, `assetInfo[token].balance` is decreased even the token balance of vault is increased. Because `assetInfo[token].balance` is used to calculate the exchangeRate  when user withdraws their token from the vault, the amount of token user can redeem will be wrongly calculated and user suffers losses. 
In extreme cases, user may loss all their tokens and withdraw nothing from the vault. All user's token asset will be locked forever.

## Vulnerability Detail
When calling `poolRepayAll()`,  `_poolRepayAll()` is called to the total amount of borrowed token(Interest included) and repay all of it. The  token balance of the vault will be increased, so will `assetInfo[token].balance`.
But in [L129](https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L129),  `assetInfo[token].balance` was wrongly decreased. 

    info.balance = info.balance - amount;

Since `assetInfo[token].balance` is used to calculate the exchange rate when calling `D3VaultFunding#userWithdraw()`, the amount of token user can redeem will be wrongly calculated and user suffers losses. 

Example of suffering losses:

1. A new `token` is added to the vault, `reserveFactor` is set to 0
2. user1 deposits 200 `token`, token balance of the vault is 200, `assetInfo[token].balance` is 200
3. user2 deposits 200 `token`, token balance of the vault is 400, `assetInfo[token].balance` is increased to 400
4. pool1 borrows 100 `token`,  token balance of the vault is 300, `assetInfo[token].balance` is decreased to 300
5. pool1 repay their debt after a while by calling `poolRepayAll()`, supposes the total repay is 120 `token`, token balance of the vault now is 420,  but `assetInfo[token].balance` is decreased to 180 because of wrong calculation.
6. user1 try to withdraw `token` from the vault with all dTokens,  the amount of token redeemed is supposed to be 210, but actually user1 can only get 90 because the wrong value of `assetInfo[token].balance` is used to calculated exchange rate.

In extreme cases, after many times of calling `poolRepayAll()`,  `assetInfo[token].balance` can be decreased to zero. Nobody can withdraw their token assets from the vault. All deposited token assets will be locked in the vault forever, at the same time no any pool can borrow this token from the vault.

## Impact
All users could suffer losses on their deposited tokens in the vault because of wrong calculation. In extreme cases,  all tokens will be locked in the vault forever and no way to withdraw them.

## Code Snippet
https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L129
## Tool used

Manual Review

## Proof of concept

Copy the below codes into test/DODOV3MM/D3Vault/D3VaultFunding.t.sol
Run forge test --match-path test/DODOV3MM/D3Vault/D3VaultFunding.t.sol to check the result.

    function testTokenIsLockedInVaultAfterPoolRepayAll() public {
        vm.prank(user1);
        token1.approve(address(dodoApprove), type(uint256).max);

        (address dToken,,,,,,,,,,) = d3Vault.getAssetInfo(address(token1));
        vm.prank(user1);
        D3Token(dToken).approve(address(dodoApprove), type(uint256).max);

        mockUserQuota.setUserQuota(user1, address(token1), 1000 * 1e8);
        vm.prank(user1);
        d3Proxy.userDeposit(user1, address(token1), 500 * 1e8);

        token1.mint(address(d3MM), 100 * 1e8);
        for (uint i=0; i<5; i++) {
            poolBorrow(address(d3MM), address(token1), 50 * 1e8);
            vm.prank(address(d3MM));
            d3Vault.poolRepayAll(address(token1));
        }

        uint cash = d3Vault.getCash(address(token1));
        assertEq(cash, 0);

        uint dTokenBalance = IERC20(dToken).balanceOf(user1);
        //the amount of dToken user1 has is 500 * 1e8 before userWithdraw()
        assertEq(dTokenBalance, 500 * 1e8);

        uint preBalance = token1.balanceOf(user1);
        vm.prank(user1);
        d3Proxy.userWithdraw(user1, address(token1), dToken, 500 * 1e8);
        uint afterBalance = token1.balanceOf(user1);
        uint tokenWithdrawn = afterBalance - preBalance;
        //the amount of token withdrawn is zero, user1 lost all their deposited token1
        assertEq(tokenWithdrawn, 0);

        dTokenBalance = IERC20(dToken).balanceOf(user1);
        //All of user's dToken has been burned after userWithdraw().
        assertEq(dTokenBalance, 0);
    }

From the above codes, all of tokens user1 deposited and the corresponding yield have been locked in the vault forever. There is no way for user to get them back.

## Recommendation

https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L129
Change the code as below:

    info.balance = info.balance + amount;
