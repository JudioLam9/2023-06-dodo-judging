skyge

high

# _poolRepayAll() function causing incorrect balance update

## Summary

The `_poolRepayAll()` function in the contract `D3VaultFunding` has an error to update the cash. This error can cause a negative balance for the `info.balance` variable, which can affect exchange rate, and leading to unexpected liquidation and other users can not withdraw their funds as expected.

## Vulnerability Detail

The error is in the line 129 `info.balance = info.balance - amount;` which should be `info.balance = info.balance + amount;`

A test case for this:

```solidity
function testPoolRepayAll() public {
    // user1 deposit 500 into vault
    // pool has 100 as collateral, then borrow 100
    // after 1 year, pools repay all.
    vm.prank(user1);
    token1.approve(address(dodoApprove), type(uint256).max);

    mockUserQuota.setUserQuota(user1, address(token1), 1000 * 1e8);
    vm.prank(user1);
    d3Proxy.userDeposit(user1, address(token1), 500 * 1e8);

    token1.mint(address(d3MM), 100 * 1e8);
    console.log("before poolBorrow, exchange rate is:", d3Vault.getExchangeRate(address(token1)), "\n");
    poolBorrow(address(d3MM), address(token1), 100 * 1e8);

    // pass one year
    vm.warp(31536000 + 1);

    vm.startPrank(address(d3MM));

    token1.approve(address(d3Vault), type(uint256).max);

    console.log("before repay all, cash is: ", d3Vault.getCash(address(token1)));
    console.log("before repay all, exchange rate is: ", d3Vault.getExchangeRate(address(token1)), "\n");
    d3Vault.poolRepayAll(address(token1));
    console.log("after repay all, cash is:  ", d3Vault.getCash(address(token1)));
    console.log("after repay all, exchange rate  is: ", d3Vault.getExchangeRate(address(token1)), "\n");

    vm.stopPrank();

    vm.prank(user1);
    
    uint256 balance1 = token1.balanceOf(user1);
    userWithdraw(user1, address(token1), 100 * 1e8);
    uint256 balance2 = token1.balanceOf(user1);
    console.log("user1 tries to withdraw 100 * 1e8, but got: ", balance2 - balance1);
}
```

result is:
```js
[PASS] testPoolRepayAll() (gas: 671133)
Logs:
  before poolBorrow, exchange rate is: 1000000000000000000 

  before repay all, cash is:  40000000000
  before repay all, exchange rate is:  1076729846580000000 

  after repay all, cash is:   25204384589
  after repay all, exchange rate  is:  484905230140000000 

  user1 tries to withdraw 100 * 1e8, but got:  4849052301
```

## Impact

The impact of this vulnerability can be severe, potentially leading to financial losses for the users of the smart contract. If the balance of the token asset decrease like above, it can affect the cash and exchange rate, which can in turn affect the equity of other user accounts and lead to their liquidation. The vulnerability should be fixed immediately to prevent any further damage.

## Code Snippet

https://github.com/sherlock-audit/2023-06-dodo/blob/main/new-dodo-v3/contracts/DODOV3MM/D3Vault/D3VaultFunding.sol#L129

```solidity
function _poolRepayAll(address pool, address token) internal {
    accrueInterest(token);

    AssetInfo storage info = assetInfo[token];
    BorrowRecord storage record = info.borrowRecord[pool];
    uint256 amount = record.amount.div(record.interestIndex == 0 ? 1e18 : record.interestIndex).mul(info.borrowIndex);

    uint256 interests = amount;

    record.amount = 0;
    record.interestIndex = info.borrowIndex;
    info.totalBorrows = info.totalBorrows - amount;
    info.balance = info.balance - amount;
    IERC20(token).safeTransferFrom(pool, address(this), amount);

    emit PoolRepay(pool, token, amount, interests);
}
```

## Tool used

Manual Review

## Recommendation

To fix this vulnerability, the line 129 `info.balance = info.balance - amount;` should be corrected to `info.balance = info.balance + amount;`
